

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Executability Conditions
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% Cannot move to a location if you are already there...
-occurs(move(R, L), I) :- holds(loc(R, L), I).

%% Cannot pick up an object if you are not in the same room...
-occurs(pickup(R, O), I) :- holds(loc(R, L1), I), holds(loc(O, L2), I), L1 != L2.
-occurs(pickup(R, O), I) :- holds(loc(R, L), I), -holds(loc(O, L), I).
-occurs(pickup(R, O), I) :- holds(loc(O, L), I), -holds(loc(R, L), I).

%% Cannot pick up an object if it already has that object (or another one) in hand.
-occurs(pickup(R, O2), I) :- holds(in_hand(R, O1), I).

%% Cannot put down an object unless it is in hand...
-occurs(putdown(R, O), I) :-  not holds(in_hand(R, O), I).

%% Cannot serve an object that is not in hand...
-occurs(serve(R, O, P), I) :- not holds(in_hand(R, O), I).

%% Cannot serve an object unless robot and human are in same location...
-occurs(serve(R, O, P), I) :- holds(loc(R, L1), I), holds(loc(P, L2), I), L1 != L2.

%% Rules to prevent incorrect serving...
-occurs(serve(R, O, P), I) :- holds(loc(R, L), I), -holds(loc(P, L), I).
-occurs(serve(R, O, P), I) :- holds(loc(P, L), I), -holds(loc(R, L), I).

%% Cannot label an item if it is not in same location.
-occurs(affix_label(R,X), I) :- holds(loc(R, L1), I), holds(loc(X, L2), I), L1 != L2.
-occurs(affix_label(R,X), I) :- holds(loc(R, L), I), -holds(loc(X, L), I).
-occurs(affix_label(R,X), I) :- holds(loc(X, L), I), -holds(loc(R, L), I).


%% Rules to be discovered...
-occurs(serve(R, O, P), I) :- role_type(P, engineer), 
			      loc_type(L, workshop), 
			      holds(loc(P, L), I).
-occurs(serve(R, O, P), I) :- holds(item_status(O, damaged), I).
-occurs(pickup(R, O), I) :- obj_weight(O, heavy).


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% State Constraints
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% An item can only have one status.
-holds(item_status(X,S1), I) :- holds(item_status(X,S2),I), S1 != S2.

%% An item is either labelled or not labelled. 
-holds(is_labelled(X,B1),I) :- holds(is_labelled(X,B2),I), B1 != B2. 

%% Any thing exists in only one location...
-holds(loc(T, L2), I) :- holds(loc(T, L1), I), L1!=L2.

%% If a robot is holding an object, they have the same location...
holds(loc(O, L), I) :- holds(loc(R, L), I), holds(in_hand(R, O), I).

%% Only one entity can have an object in hand...
-holds(in_hand(E2, O), I) :- holds(in_hand(E1, O), I), E1 != E2.

%% Only one object can be held at any time...
-holds(in_hand(E, O2), I) :- holds(in_hand(E, O1), I), O1 != O2.

%% Attributes all have unique values...
-loc_type(L, T2) :- loc_type(L, T1), T1 != T2.
-role_type(P, RT2) :- role_type(P, RT1), RT1 != RT2.
-obj_weight(O, W2) :- obj_weight(O, W1), W1 != W2.
-has_surface(X,S2) :- has_surface(X,S1), S1 != S2.
-has_arm_type(R,T2) :- has_arm_type(R,T1), T1 != T2.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Inertial axiom + CWA
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% General inertia axioms...
holds(F,I+1) :- #inertial_fluent(F),
                holds(F,I),
                not -holds(F,I+1).

-holds(F,I+1) :- #inertial_fluent(F),
                 -holds(F,I),
                 not holds(F,I+1).
                 
%% CWA for actions...
-occurs(A,I) :- not occurs(A,I).

%% CWA for exogenous actions...
-exogenous(E,I) :- not exogenous(E,I).

%% CWA for defined fluents...
%-holds(F, I) :- #defined_fluent(F), not holds(F, I). 



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%       
%% Default and CR rules

% Unclear if these actually work for e.g. multiple libraries or offices

%% Books are usually in a library...
holds(loc(B, L), 0) :- #book(B), loc_type(L, library), 
			     not -holds(loc(B, L), 0).

%% Books are otherwise in an office...
holds(loc(B, LOff), 0) :- #book(B),
					 loc_type(LLib, library), 
					 loc_type(LOff, office), 
		       	     -holds(loc(B, LLib), 0),
 		             not -holds(loc(B, LOff), 0).

is_defined(loc(B, L)) :- #book(B), loc_type(L, library).

%% Under exceptional circumstances, assume books are elsewhere...
-holds(loc(B, L), 0) :+ #book(B), loc_type(L, library).
-holds(loc(B, LOff), 0) :+ #book(B), loc_type(LOff, office), 
			     loc_type(LLib, library), 
			     -holds(loc(B, LLib), 0).

				 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%       
%% History and initial state rules

%% Take what actually happened into account...
occurs(A,I) :- hpd(A,I).

%% Reality check axioms...
:- obs(F, true, I), -holds(F, I).
:- obs(F, false, I), holds(F, I).

is_defined(F) :- obs(F, Y, 0).
-holds(F, 0) :- #inertial_fluent(F),
		not is_defined(F), not holds(F, 0).

%% Awareness axiom...
%holds(F, 0) | -holds(F, 0) :- #inertial_fluent(F).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Planning Module...
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% Failure is not an option...
success :- goal(I).
:- not success. 

%% Cannot be idle while goal remains unachieved...
occurs(A, I) | -occurs(A, I) :- not goal(I). 

%% Cannot execute two actions at the same time...
:- occurs(A1,I), occurs(A2,I), A1 != A2.

something_happened(I) :- occurs(A, I).

:- not goal(I),
   not something_happened(I).

%:- goal(I), goal(I-1),
%   J < I,
%   not something_happened(J).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Test
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


